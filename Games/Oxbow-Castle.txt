include "inc/Foundation.md";

#
# Outer court rooms
#
item outside;
item outer_court_SW;
item outer_court_S;
item outer_court_SE;
item stable;
item outer_court_W;
item outer_court_center;
item outer_court_NW;
item outer_court_N;
item outer_court_NE;

#
# Church rooms
#
item church_yard_W;
item church_yard_S;
item church_yard_N;
item church_W;
item church_E;
item side_chapel;
item priest_house;
item priest_key;
item church_key;
item priest_house_trunk;
item priest_house_desk;
item priest_house_paper;

#
# Inner court rooms
#
item inner_gate;
item inner_court_S;
item inner_court_SW;
item inner_court_SE;
item inner_court_N;
item inner_court_NW;
item inner_court_NE;

command "help"
{
    Message("Following are commands you can say:");
    ListCommands();
}

game
{
    # Welcome message at the start of the game.
    Message("# Welcome to Oxbow Castle");
    Message(
        "You are a messenger sent by King Peregrin the Paranoid to find out what has "
        "become of Oxbow castle. Previous messengers have not returned, and the king "
        "has begin to suspect the castle lord of plotting against him."
    );
    Message(
        "That is one possibility, but you have also heard stranger rumors. In the "
        "taverns, people whisper that something dark and sinister has befallen the "
        "place. Of course, it may just be the ale talking. You are here to find out!"
    );
    Message(
        "Oxbow Castle is situated in a sharp bend of the river Ox, such that it is "
        "surrounded on three sides by water. It is late morning, and you have just "
        "arrived at the only entrance to the castle, an open iron gate in the south "
        "wall. It is strangely silent. No one seems to be around."
    );
    Message(
        "At any time you can type 'help' to see a list of commands you can say."
    );

    player.Location = outside;
}

# Use action for a well.
function UseWell($item:Item)
{
    Message("You draw water from the well and take a refreshing drink.");
}

# Helper function create a well in a specified location.
function NewWell($loc:Item)
{
    var $well = NewItem($"{$loc}_well");
    $well.Location = $loc;
    $well.Noun = "well";
    $well.UseAction = UseWell;
}

game
{
    # Outer court descriptions
    outside.Description = "You are outside the main gate of Oxbow Castle.";
    outer_court_S.Description = 
        "You are at the south end of a large courtyard, which extends to the North, East, and West. "
        "At the north end of the courtyard you can see the high walls of the keep.";
    outer_court_SW.Description =
        "You are at the southwest corner of a large outer courtyard, which extends to the North and East. "
        "The south and west walls meet in a small round tower, which is open to the inside. There is nothing "
        "in the tower.";
    outer_court_SE.Description = 
        "You are in the southeast corner of a large outer courtyard, which extends to the West. The way north "
        "is blocked by a stone wall behind which you can see a tall stone church. The south and east walls meet "
        "in a small round tower, which is open to the inside. There is nothing in the tower.";
    stable.Description =
        "You are in a large wooden stable with room for many horses. Neither hay nor horses are to be found "
        "here now, only their lingering smell.";
    outer_court_W.Description = 
        "You are at the west side of a large outer courtyard, which extends to the North, South, and East. "
        "Against the wall to your west is a wooden stable.";
    outer_court_center.Description =
        "You are in the center of a large outer courtyard, which extends to the North, West, and South. East "
        "of you is a stone wall with a wooden gate behind which you can see a tall stone church.";
    outer_court_NW.Description =
        "You are in the northwest corner of a large outer courtyard, which extends to the South and East. "
        "The high wall of the keep looms above you to the North.";
    outer_court_N.Description = 
        "You are at the north end of a large outer courtyard, which extends to the West, East, and South. "
        "Looming above you to the north is the keep's gatehouse. Its large, round stone towers jut out on "
        "either side of an open portcullis.";
    outer_court_NE.Description =
        "You are in the northeast corner of a large outer courtyard, which extends to the West. The high wall "
        "of the keep looms above you to the North. The way south is blocked by a stone wall behind which you "
        "can see a tall stone church.";

    # Outer court northward links
    NewDoorItem(outside, outer_court_S, Direction.North, "iron", "gate", DoorState.Open);
    NewLink(outer_court_SW, outer_court_W, Direction.North);
    NewLink(outer_court_S, outer_court_center, Direction.North);
    NewLink(outer_court_W, outer_court_NW, Direction.North);
    NewLink(outer_court_center, outer_court_N, Direction.North);

    # Outer court eastward links
    NewLink(outer_court_SW, outer_court_S, Direction.East);
    NewLink(outer_court_S, outer_court_SE, Direction.East);
    NewClosedDoor(stable, outer_court_W, Direction.East);
    NewLink(outer_court_W, outer_court_center, Direction.East);
    NewLink(outer_court_NW, outer_court_N, Direction.East);
    NewLink(outer_court_N, outer_court_NE, Direction.East);

    NewWell(outer_court_NW);
}

game
{
    # Church descriptions.
    church_yard_W.Description =
        "You are in a walled church yard. East of you is the arched doorway of a beautiful stone "
        "church. The church yard wraps around the church to the North and South.";
    church_yard_S.Description = 
        "You are in a walled church yard south of the church. The church yard wraps around the church "
        "to the North. East of you is a smaller stone building adjoining the church.";
    church_yard_N.Description =
        "You are in a walled churcn yard north of the church. The church yard wraps around the church "
        "to the South. There are graves here.";
    church_W.Description =
        "You are at the west end of the church. A long aisle runs from here to the East, with fluted "
        "stone columns on either side. Windows set high in the walls let in light.";
    church_E.Description =
        "You are at the east end of the church. A long aisle runs from here to the West. Between "
        "fluted columns to the North, you can see a side chapel.";
    side_chapel.Description =
        "You are in a side chapel with statues of saints. An opening to the South leads to the "
        "main part of the church.";
    priest_house.Description =
        "You are in a one-room house, presumably the priest's residence.";

    # Church eastward links.
    NewDoorItem(outer_court_center, church_yard_W, Direction.East, "wooden", "gate", DoorState.Closed);
    NewDoorItem(church_yard_W, church_W, Direction.East, "carved wooden", "door", DoorState.Closed).Key = church_key;
    NewLink(church_W, church_E, Direction.East);
    NewDoorItem(church_yard_S, priest_house, Direction.East, "oak", "door", DoorState.Locked).Key = priest_key;

    # Church north links.
    NewLink(church_yard_S, church_yard_W, Direction.North);
    NewLink(church_yard_W, church_yard_N, Direction.North);
    NewLink(church_E, side_chapel, Direction.North);
    NewDoorItem(priest_house, church_E, Direction.North, "small wooden", "door", DoorState.Closed);

    # Priest house items.
    NewTable("lumpy", "bed", priest_house);
    InitializeContainer(priest_house_trunk, "pine", "trunk", DoorState.Closed, priest_house);
    NewPortableItem("black", "robe", priest_house_trunk);
    InitializeKey(priest_key, "crude iron", "key", priest_house_trunk);
    InitializeKey(church_key, "ornate brass", "key", priest_house_trunk);
    InitializeTable(priest_house_desk, "large oak", "desk", priest_house);
    NewPortableItem("white", "paper", priest_house_desk).Description =
        "The paper appears to be a draft of a sermon. Between the scratched-out words and ink blots, "
        "you read a passionate diatribe criticizing the congregation for continuing to believe in the "
        "old gods, as well as supernatural creatures like fairies.";
}

function OnEnterKeep($from:Item, $to:Item) : Bool
{
    if ($from == outer_court_N && $to == inner_court_S)
    {
        Message(
            "You pass through the open portcullis into a dim tunnel. High in the walls on "
            "either side are arrow loops through which defenders could rain down fire on "
            "an attacker, and in the ceiling above you are murder holes through which "
            "boulders could be dropped. However, no one challenges you or raises an alarm."
            );
        Message(
            "At the north end of the tunnel, you pass through another open gate into an "
            "inner courtyard. For a moment, the air around you seems to shimmer, but "
            "perhaps you are only dazzled by the sun after leaving the dim tunnel."
        );
    }
}

game
{
    # One-way link from outer court to inner court via. inner_gate.
    LinkRoomsOneWay(outer_court_N, inner_court_S, inner_gate, Direction.North);
    outer_court_N.LeaveAction = OnEnterKeep;

    # Inner court descriptions
    inner_court_S.Description =
        "You are at the south end of the inner courtyard, which extends to the "
        "North, East, and West. South of you is the main gatehouse of the keep "
        "with its inner gate open. Across the courtyard to the North is the "
        "great hall. Other buildings surround the courtyard on all sides.";
    inner_court_SW.Description =
        "You are in the southwest corner of the inner courtyard, which extends to "
        "the North and East.";
    inner_court_SE.Description =
        "You are in the southeast corner of the inner courtyard, which extends to "
        "the North and West.";
    inner_court_N.Description =
        "You are at the north end of the inner courtyard, which extends to the "
        "South, East, and West.";
    inner_court_NW.Description =
        "You are in the northwest corner of the inner courtyard, which extends "
        "to the South and East.";
    inner_court_NE.Description =
        "You are in the northeast corner of the inner courtyard, which extends "
        "to the South and West.";
    
    # Inner court northward links
    NewLink(inner_court_S, inner_court_N, Direction.North);
    NewLink(inner_court_SW, inner_court_NW, Direction.North);
    NewLink(inner_court_SE, inner_court_NE, Direction.North);

    # Inner court eastward links
    NewLink(inner_court_SW, inner_court_S, Direction.East);
    NewLink(inner_court_S, inner_court_SE, Direction.East);
    NewLink(inner_court_NW, inner_court_N, Direction.East);
    NewLink(inner_court_N, inner_court_NE, Direction.East);

    NewWell(inner_court_NE);
}
