include "inc/Foundation.md";

item outside_gate;
item main_gate;

item gate_room;

item E_guard_room;
item E_guard_room_cabinet;

item W_guard_room;
item W_guard_room_chest;
item W_guard_room_door;

item SE_tower_0;

item SW_tower_0;

item SW_square;

item S_square;

item SE_square;

item NW_square;

item N_square;

item NE_square;

# Special behavior when entering the gate room from outside.
function OnEnterGateRoom($from:Item, $to:Item) : Bool
{
    if ($from == outside_gate)
    {
        Message(
            "You pass under the iron portcullis. With a crash, it slides down from "
            "above and slams shut behind you. You hear the click of bolts sliding into "
            "place. You are locked in."
        );
    }
    main_gate.DoorState = DoorState.Locked;
}

# Special behavior when exiting the castle.
function OnLeaveCastle($from:Item, $to:Item) : Bool
{
    if ($from == gate_room)
    {
        Message("You have escaped Gloom Castle!");
        Message("Congratulations! You win the game!");
        EndGame(true);
    }
}

command "help"
{
    Message("Following are commands you can say:");
    ListCommands();
}

game
{
    # Welcome message at the start of the game.
    Message("# Welcome to Gloom Castle");
    Message(
        "You are a messenger sent by King Peregrin the Paranoid to find out what has "
        "become of Gloom castle. Previous messengers have not returned, and the king "
        "has begin to suspect the castle lord of plotting against him."
    );
    Message(
        "That is one possibility, but you have also heard stranger rumors. In the "
        "taverns, the name Gloom Castle elicits whispers of monsters or supernatural "
        "forces. Of course, it may just be the ale talking. You are here to find out."
    );
    Message(
        "It is now late morning and you have just arrived outside the south wall of the "
        "castle. Before you is an arched gateway tall enough for a mounted knight to "
        "ride through. Its massive iron portcullis is open at the moment, and no one "
        "seems to be around."
    );
    Message(
        "At any time you can type 'help' to see a list of commands you can say."
    );

    # Outside gate (starting point)
    player.Location = outside_gate;
    outside_gate.Description = "You are standing outside the South wall of the castle.";
    outside_gate.EnterAction = OnLeaveCastle;

    # Main gate
    InitializeDoor(main_gate, "massive", "iron", "portcullis", DoorState.Open);
    LinkRooms(outside_gate, gate_room, main_gate, Direction.North);
    gate_room.EnterAction = OnEnterGateRoom;

    # Gate room
    gate_room.Description = "You are in a cobbled stone gate room.";
    NewDoorItem(gate_room, E_guard_room, Direction.East, "door", DoorState.None).Adj1 = "smashed";

    # East guard room
    E_guard_room.Description = "You are in what appears to be a guard room.";
    InitializeContainer(E_guard_room_cabinet, "tall", "wooden", "cabinet", DoorState.Closed, E_guard_room);
    NewCandle("bronze", "oil", "lamp", E_guard_room_cabinet);
    NewWeapon("notched", null, "sword", 10, E_guard_room_cabinet);
    W_guard_room_door.Key = NewKey("large", "iron", "key", E_guard_room_cabinet);
    NewKey("bent", "brass", "key", E_guard_room_cabinet);
    NewClosedDoor(E_guard_room, SE_tower_0, Direction.East);

    # Southeast tower level 0
    SE_tower_0.Description = "You are in a square stone room.";
    SE_tower_0.IsDark = true;

    # West Guard Room
    W_guard_room.Description = "You are in what appears to be a guard room.";
    InitializeDoor(W_guard_room_door, "oak", null, "door", DoorState.Locked);
    LinkRooms(W_guard_room, gate_room, W_guard_room_door, Direction.East);
    InitializeContainer(W_guard_room_chest, "heavy", "pine", "chest", DoorState.Locked, W_guard_room);
    W_guard_room_chest.Description = "The heavy pine chest has a brass lock.";
    SetHealth(W_guard_room_chest, 50, 50);
    NewLighter("small", null, "tinderbox", W_guard_room_chest);
}
